plugins {
    id 'war'
    id 'java'
    id 'jacoco'
    id 'application'
    id 'maven-publish'
    id 'co.uzzu.dotenv.gradle' version '1.1.0'
    id 'de.undercouch.download' version '4.1.1'
    id 'org.springframework.boot' version '2.5.1'
    id 'com.github.ben-manes.versions' version '0.39.0'
    id 'com.github.dawnwords.jacoco.badge' version '0.2.4'
}

group = 'org.bbaw.aaew.tla'
version = '2.0'
sourceCompatibility = '11'

ext {
    fontawesomeVersion = env.FONTAWESOME_VERSION.orElse('5.12.1')
    bootstrapVersion = env.BOOTSTRAP_VERSION.orElse('4.6.0')
    jqueryVersion = env.JQUERY_VERSION.orElse('3.6.0')
    cookieJsVersion = env.COOKIEJS_VERSION.orElse('2.2.1')
    cookieBannerVersion = env.COOKIE_BANNER_VERSION.orElse('1.2.2')
    fontawesomeSrc = "https://use.fontawesome.com/releases/v${fontawesomeVersion}/fontawesome-free-${fontawesomeVersion}-web.zip"
    bootstrapSrc = "https://github.com/twbs/bootstrap/releases/download/v${bootstrapVersion}/bootstrap-${bootstrapVersion}-dist.zip"
    jquerySrc = "https://code.jquery.com/jquery-${jqueryVersion}.min.js"
    cookieJsSrc = "https://github.com/js-cookie/js-cookie/releases/download/v${cookieJsVersion}/js.cookie-${cookieJsVersion}.min.js"
    cookieBannerSrc = "https://cdn.jsdelivr.net/gh/dobarkod/cookie-banner@${cookieBannerVersion}/dist/cookiebanner.min.js"
	staticDir = 'src/main/resources/static'
    vendorDir = "${staticDir}/vendor"
    
}

def assets = [
  'cookieJs' : [cookieJsSrc, 'js.cookie.min.js'],
  'cookieBanner': [cookieBannerSrc, 'cookiebanner.min.js'],
  'jquery': [jquerySrc, 'jquery.min.js'],
  'headroomJs': ['https://unpkg.com/headroom.js/dist/headroom.min.js', 'headroom.min.js']
]

def frameworks = [
  'bootstrap': [bootstrapSrc, "${staticDir}/vendor/bootstrap-${bootstrapVersion}-dist"],
  'fontawesome': [fontawesomeSrc, "${staticDir}/vendor/fontawesome-free-${fontawesomeVersion}-web"]
]

publishing {
    publications {
        maven(MavenPublication) {
            artifactId = 'tla-web'
            pom {
                name = 'TLA Web Frontend'
                description = 'HTML frontend for the Thesaurus Linguae Aegyptiae web component'
            }
            from components.java
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.spring.io/milestone' }
}

configurations {
    // don't embed all the servlet containers 
    implementation.exclude module: 'spring-boot-starter-jetty'
    implementation.exclude module: 'spring-boot-starter-netty'

    testImplementation.exclude group: 'org.junit.vintage'
    testImplementation.exclude module: 'junit-vintage-engine'
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    implementation 'org.modelmapper:modelmapper:2.4.4'
    implementation 'org.jooq:jool:0.9.14'

    implementation 'org.springframework.boot:spring-boot-starter-web:2.5.1'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf:2.5.1'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:2.5.3'
    implementation 'org.slf4j:slf4j-simple:2.0.0-alpha1'
    implementation 'org.apache.commons:commons-lang3:3.12.0'

    implementation 'com.github.thesaurus-linguae-aegyptiae:tla-common:master-SNAPSHOT'
    implementation 'com.github.rosmord.jsesh:jsesh:release-7.5.5'
    implementation 'com.github.rosmord.jsesh:jseshGlyphs:release-7.5.5'

    testImplementation 'org.seleniumhq.selenium:selenium-remote-driver:4.0.0-beta-4'
    testImplementation 'org.seleniumhq.selenium:selenium-chrome-driver:4.0.0-beta-4'
    testImplementation 'org.seleniumhq.selenium:selenium-support:4.0.0-beta-4'
    testImplementation 'org.testcontainers:selenium:1.15.3'
    testImplementation 'org.testcontainers:junit-jupiter:1.15.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-test:2.5.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.0-M1'
}

task installAssets {
    description = 'Download and install third-party JS/CSS frameworks and libraries'
    dependsOn 'installFontRoboto'
    outputs.files(
        file("${staticDir}/vendor/")
    )
}

task updateAssets {
    group = 'Init'
    description = 'Removes and re-installs third-party ressources added with :installAssets.'
    dependsOn 'cleanAssets'
    dependsOn 'installAssets'
}

def createDownloadAssetTask(String name, String url, String destFile) {
  return tasks.create("download${name.capitalize()}", Download) {
    doFirst {
      mkdir "${staticDir}/vendor"
    }
    description = "Download && install ${name}"
    src "${url}"
    dest file("${staticDir}/vendor/${destFile}")
    onlyIfModified true
  }
}

assets.each {
  entry -> installAssets.dependsOn createDownloadAssetTask(
    entry.key, entry.value[0], entry.value[1]
  )
}

frameworks.each {
  entry -> installAssets.dependsOn installFrameworkTask(
    entry.key, entry.value[0], entry.value[1]
  )
}

task downloadFontRoboto(type: Download) {
    group = 'Init'
    description = 'Download Google Roboto font'
    src 'https://google-webfonts-helper.herokuapp.com/api/fonts/roboto?download=zip&subsets=latin&variants=300,500,700,300italic,regular,italic,700italic'
    dest new File(buildDir, 'roboto.zip')
    onlyIfModified true
}

def downloadFrameworkTask(String name, String url) {
    return tasks.create("download${name.capitalize()}", Download) {
      description = "Downloads ${name.capitalize()} distrubution into build directory."
      src "${url}"
      dest new File(buildDir, "${name}.zip")
      onlyIfModified true
      outputs.files(
        new File(buildDir, "${name}.zip")
      )
    }
}

def installFrameworkTask(String name, String url, String dir) {
    return tasks.create("install${name.capitalize()}", Copy) {
      inputs.files(
        new File(buildDir, "${name}.zip")
      )
      group = 'Init'
      description = "Installs ${name.capitalize()} distribution into assets directory."
      dependsOn downloadFrameworkTask(name, url)
      doFirst {
        mkdir "${vendorDir}"
      }
      if (!file("${vendorDir}/${name}").exists()) {
        from zipTree(
          new File(buildDir, "${name}.zip")
        )
        into file("${vendorDir}")
        doLast {
            file("${dir}").renameTo(
              file("${vendorDir}/${name}")
            )
        }
      }
      outputs.files(
        file("${vendorDir}/${name}")
      )
    }
}

task installFontRoboto(dependsOn: downloadFontRoboto, type: Copy) {
    group = 'Init'
    description = 'Installs Google Roboto font'
    from zipTree(downloadFontRoboto.dest)
    into file("${staticDir}/font")
}

task install {
    inputs.files(
      file('.env')
    )
    group = 'Init'
    description = 'Installs additional resources required for running the app'
    dependsOn 'installAssets'
    outputs.files(
      file("${staticDir}/vendor/")
    )
}

task cleanAssets(type: Delete) {
    group = 'Init'
    description = 'Delete files added with :installAssets'
    dependsOn 'cleanInstallAssets'
    frameworks.each {
      entry -> cleanAssets.dependsOn "cleanDownload${entry.key.capitalize()}"
    }
    delete "${staticDir}/vendor/"
}

clean {
    dependsOn 'cleanInstall'
}

springBoot {
    buildInfo()
}

bootJar {
    dependsOn 'install'
}

def installationReminder() {
  if ( !file("${staticDir}/vendor/").exists() ) {
    println ' ======================================'
    println ' !!!  Must run task :install first: !!!'
    println ' !!!                                !!!'
    println ' !!!     ./gradlew.bat install      !!!'
    println ' ======================================'
    throw new StopExecutionException()
  }
}


bootRun {
    systemProperty 'spring.profiles.active', 'dev'
    doFirst {
      installationReminder()
    }
}

application {
    mainClass = 'tla.web.App'
}

test {
    useJUnitPlatform {
      includeTags '!selenium'
    }
    finalizedBy 'jacocoTestReport'
}

task selenium(type: Test) {
    group = 'Verification'
    description = 'Runs functional tests of the web UI using a Selenium container.'
    systemProperty 'spring.profiles.active', 'selenium'
    doFirst {
      installationReminder()
    }
    useJUnitPlatform {
      includeTags 'selenium'
    }
}

jacocoTestReport {
    reports {
        xml.required.set(true)
        html.required.set(true)
    }
    finalizedBy 'generateJacocoBadge'
}

